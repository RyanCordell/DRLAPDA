#define PDA_ChangeDelay 4
#define PDA_ScrollDelay 2
#define HOLD_MAX 15

/** Thanks to HeXaGoN for the maths behind the mouse cursor here! */
SCRIPT "PDA_MouseController" (void)
{
    const int anymask =
        BT_FORWARD | BT_BACK | BT_ATTACK | BT_ALTATTACK | BT_MOVERIGHT | BT_RIGHT | BT_MOVELEFT | BT_LEFT;
    int buttons[MAX_PLAYERS], oldbuttons[MAX_PLAYERS];
    
    int MIN_HUD_W = 320,
        MIN_HUD_Y = 200;
        
    str graphic[MAX_PLAYERS] = {"","","","","","","",""};

    #define localCursor graphic[PlayerNumber()]
    
    fixed xScale, yScale;
    fixed mx = (MIN_HUD_W/2),
          my = (MIN_HUD_Y/2);
          
    int oldMouseY = 0;
    
    if (GameType() == GAME_SINGLE_PLAYER)
        inverted = GetCVar("invertmouse");

    //#define sensitivity 8
    
    const int HMIN = 0 + PDA_SCREEN_X;
    const int VMIN = 0 + PDA_SCREEN_Y;
    const int HMAX = PDA_SCREEN_WIDTH  + PDA_SCREEN_X - 7;
    const int VMAX = PDA_SCREEN_HEIGHT + PDA_SCREEN_Y - 7;
    
    int PDA_X_sensitivity = GetCvar("DRLA_PDA_sensitivity_x"),
        PDA_Y_sensitivity = GetCvar("DRLA_PDA_sensitivity_y");
    
    int vBuffer = 0;
    int myoff;
    const int ACTIVATOR = -1;
    
    SetHudSize(MIN_HUD_W, MIN_HUD_Y, 0);
    // SetHudClipRect(
    //     PDA_SCREEN_X, PDA_SCREEN_Y, 
    //     PDA_SCREEN_WIDTH, PDA_SCREEN_HEIGHT, 
    //     PDA_SCREEN_WIDTH
    // );

    #define setScrollDelta() (PDA_Local.scrollingY > PDA_Local.oldScrollingY) ? 1 : (PDA_Local.scrollingY == PDA_Local.oldScrollingY) ? 0 : -1;

    while (PDA_Local.active)
    {
                                     buttons[PlayerNumber()] = GetPlayerInput(ACTIVATOR, INPUT_BUTTONS);
        PDA_LocalMouse.oldState = oldbuttons[PlayerNumber()] = GetPlayerInput(ACTIVATOR, INPUT_OLDBUTTONS);
          
        oldMouseY = PDA_LocalMouse.y;
        
        PDA_LocalMouse.m1 = (buttons[PlayerNumber()] & (_ACTION)) ? true : false;
        PDA_LocalMouse.m2 = (buttons[PlayerNumber()] & (_BACK))   ? true : false;

        log("buttons (m1) %d: | bt_a %d: | bt_u %d: | _act %d:", buttons[PlayerNumber()], buttons[PlayerNumber()] & BT_ATTACK, buttons[PlayerNumber()] & BT_USE, buttons[PlayerNumber()] & (_ACTION));

        // kd: I AM REINFORCEMENTS FROM A DIFFERENT DIMENSION
        fixed mouse_sensitivity    = GetOddSensitivityCVarFixed("mouse_sensitivity", 1.0);
        fixed x_sensitivity        = GetOddSensitivityCVarFixed("m_sensitivity_x",   2.5);
        fixed y_sensitivity        = GetOddSensitivityCVarFixed("m_sensitivity_y",   5.0);
        fixed total_sensitivity    = max(x_sensitivity, y_sensitivity) * mouse_sensitivity;
        
        xScale = GetCVarFixed("m_yaw")   * total_sensitivity;
        yScale = GetCVarFixed("m_pitch") * total_sensitivity;
        // kd: aight now back to Ryan coding
        
        /*
            Cursor should:
                DEFAULT when ???
                POINTER when over a clickable object
                GRAB    when idle
                HOLDER  when dragging screen around
        */
        fixed playerX = GetPlayerInputFixed(ACTIVATOR, INPUT_YAW)   - (GetPlayerInputFixed(ACTIVATOR, INPUT_SIDEMOVE)    / PDA_X_sensitivity);
        fixed playerY = GetPlayerInputFixed(ACTIVATOR, INPUT_PITCH) + (GetPlayerInputFixed(ACTIVATOR, INPUT_FORWARDMOVE) / PDA_Y_sensitivity);

        PDA_LocalMouse.x -= (playerX << PDA_X_sensitivity) * xScale;

        if (inverted) {
            PDA_LocalMouse.y += (playerY << PDA_Y_sensitivity) * yScale;
        } else {
            PDA_LocalMouse.y -= (playerY << PDA_Y_sensitivity) * yScale;
        }

        if (PDA_LocalMouse.x != PDA_LocalMouse.oldX || PDA_LocalMouse.y != PDA_LocalMouse.oldY) 
            PDA_LocalMouse.moving = true;
        else {
            PDA_LocalMouse.moving = false;
            PDA_LocalMouse.scrolling = false;
        }

        // log("scrollY %f: | minY %f: | maxY %f: | s? %d: | m? %d:", PDA_Local.scrollingY, VMIN, PDA_Local.max_text_y, PDA_LocalMouse.scrolling, PDA_LocalMouse.moving);

        if (PDA_LocalMouse.x < HMIN) PDA_LocalMouse.x = HMIN;
        if (PDA_LocalMouse.x > HMAX) PDA_LocalMouse.x = HMAX;

        if (PDA_LocalMouse.y < VMIN) {
            PDA_LocalMouse.y = VMIN;

            if (PDA_Local.scrollingY < PDA_SCREEN_Y) {
                PDA_LocalMouse.scrolling = true;
                PDA_Local.scrollingY += 2.0 * yScale;
                PDA_Local.scrollYDelta = setScrollDelta();
            }
        }
        if (PDA_LocalMouse.y > VMAX) {
            PDA_LocalMouse.y = VMAX;

            if (PDA_Local.scrollingY > PDA_Local.max_text_y) {
                PDA_LocalMouse.scrolling = true;
                PDA_Local.scrollingY -= 2.0 * yScale;
                PDA_Local.scrollYDelta = setScrollDelta();
            }
        }
        
        if (PDA_LocalMouse.state == mouse_default) localCursor = (buttons[PlayerNumber()] & _ACTION) ? "PDACURS" : "PDACUR";
   else if (PDA_LocalMouse.state == mouse_pointer) localCursor = "PDAPNT";
   else if (PDA_LocalMouse.state == mouse_grab)    localCursor = "PDAGRB";
   else if (PDA_LocalMouse.state == mouse_holding) localCursor = "PDAHLD";
        
        if (!PDA_Local.pongrunning)
        {
            DisplayIcon(localCursor, PDA_CURSORID, PDA_LocalMouse.x + 0.1, PDA_LocalMouse.y + 0.1, 0, (buttons[PlayerNumber()] & _ACTION) ? 0.8 : 1);
        }
        
        if (holdingAction)
        {
            vBuffer                  = PDA_LocalMouse.y - oldMouseY;
            myoff                    = vBuffer;
            PDA_Local.scrollingY    += vBuffer;
            PDA_Local.scrollYDelta   = setScrollDelta();
            PDA_LocalMouse.scrolling = true;
            
            if (PDA_Local.scrollingY < PDA_Local.max_text_y) PDA_Local.scrollingY = PDA_Local.max_text_y;
            if (PDA_Local.scrollingY > VMIN)                 PDA_Local.scrollingY = VMIN; 
        }

        if (notHoldingAction) {
            PDA_LocalMouse.scrolling = false;
        }

        if (PDA_LocalMouse.scrolling) {
            if (PDA_Local.scrollingY < PDA_Local.max_text_y || PDA_Local.scrollingY > VMIN) {
                PDA_LocalMouse.scrolling = false;
            }
        }
        
        // Right mouse click
        if (pressedBack)
        {
            PDA_LocalMouse.state = mouse_default;
            PDA_PlaySound("UI/PDA/Cancel", "pda/soldier", 80);
            
            // Step back into previous page
            if (PDA_Local.category == PDA_ASSEMBLY_DETAIL) {
                PDA_SetCategory(PDA_ASSEMBLIES);
            }
            else if (PDA_Local.category == PDA_BESTIARY && PDA_Local.page > 1) {
                PDA_SetPage(1);
            }
            else if (PDA_Local.category == PDA_BESTIARY_DETAIL) {
                PDA_SetCategory(PDA_BESTIARY);
                PDA_SetPage(PDA_Local.previous_page);
            }
            else if (PDA_Local.category == PDA_PONG) {
                // ACS_NamedExecute("QuitPong",0);
                PDA_SetCategory(PDA_MISC);
            }
            else if (PDA_Local.category == PDA_HELP) {
                SetUserCVar(PlayerNumber(), "DRLA_PDA_firsttime", false);
                PDA_SetCategory(PDA_MENU);
                PDA_SetPage(1);
            }
            else if (PDA_Local.category == PDA_CREDITS) {
                PDA_SetCategory(PDA_MENU);
                PDA_SetPage(1);
            }
            else {
                PDA_SetCategory(PDA_MENU);
                PDA_SetPage(1);
            }
            PDA_ResetScroll();
        }
        
        Delay(1);
        PDA_Local.oldScrollingY = PDA_Local.scrollingY;
        PDA_LocalMouse.moving = false;
        PDA_LocalMouse.oldX = PDA_LocalMouse.x;
        PDA_LocalMouse.oldy = PDA_LocalMouse.y;

        vBuffer = 0;
        PDA_LocalMouse.m1 = false;
        PDA_LocalMouse.m2 = false;
    }
}

// EOF