#define asciiNumberZero 48

#define getNumber(a, i)  (int)(GetChar((a), (i)) - asciiNumberZero)

SCRIPT "DRLA_AssemblyGlobalizer" ENTER {
	bool dowrite;

    /** Fetch all available assembly and exotic mod effect data from ini */
    DRLA_FetchStoredInfo[PlayerNumber()] = GetUserCVarString(PlayerNumber(), "DRLA_knownassemblies");
    DRLA_FetchExoticInfo[PlayerNumber()] = GetUserCVarString(PlayerNumber(), "DRLA_knownexoticeffects");

    /**
     * READ
     * 
     * Read data from ini fetched cvars and give the player all the assemblies.
     */
    /** ASSEMBLIES */
    for(int assemblyIndex = 0; assemblyIndex < DRLA_ASSEMBLYMAX; assemblyIndex++) {
         if (stricmp(DRLA_Assemblies[assemblyIndex][0],"null") != 0) {
            if (getNumber(DRLA_FetchStoredInfo[PlayerNumber()], assemblyIndex) == 0) {
                TakeInventory(DRLA_Assemblies[assemblyIndex][0], 1);
            }
            else {
                GiveInventory(DRLA_Assemblies[assemblyIndex][0], 1);
            }
        }

        DRLA_OldAssemblyState[PlayerNumber()][assemblyIndex] = CheckInventory(DRLA_Assemblies[assemblyIndex][0]);
    }
    /** UNIQUE, LEGENDARY, DEMONIC */
    for (int effectIndex = 0; effectIndex < DRLA_EXOTICEFFECTS_MAX; effectIndex++) {
        /** Don't bother with data that's null, like Unmaker exotic effects or missing weapons */
        if (stricmp(DRLA_UniqueExoticModEffects[effectIndex][1], "null") != 0) {
            for (int bit = 1, exoticMod = 1; bit < 5; bit *= 2, exoticMod++) {
                SetInventory(DRLA_UniqueExoticModEffects[effectIndex][exoticMod], ((GetChar(DRLA_FetchExoticInfo[PlayerNumber()], effectIndex) - asciiNumberZero) & bit) - (bit-1));
            }
        }
        DRLA_OldExoticState[PlayerNumber()][effectIndex] = CheckInventory(DRLA_UniqueExoticModEffects[effectIndex][1]);
    }

    /**
     * UPDATE
     * 
     * Update old state with what was read from the ini. Any change afterwards will trigger an ini write.
     */
    while (true) {
        // ASSEMBLIES ---
        for(int assemblyIndex2 = 0; assemblyIndex2 < DRLA_ASSEMBLYMAX; assemblyIndex2++) {
            DRLA_AssemblyState[PlayerNumber()][assemblyIndex2] = CheckInventory(DRLA_Assemblies[assemblyIndex2][0]);

            if (DRLA_AssemblyState[PlayerNumber()][assemblyIndex2] != DRLA_OldAssemblyState[PlayerNumber()][assemblyIndex2]) {
                dowrite = true;
            }
        }
        // UNIQUE+ ---
        for (int weaponIndex = 0; weaponIndex < DRLA_EXOTICEFFECTS_MAX; weaponIndex++) { /** Per weapon.. */
            for (int exoticModIndex = 1, bit = 1; exoticModIndex < DRLA_EXOTICELEMENTS; bit *= 2, exoticModIndex++) { /** Per exotic mod effect.. */
                if (CheckInventory(DRLA_UniqueExoticModEffects[weaponIndex][exoticModIndex]) == 1) {
                    DRLA_KnownExoticState[PlayerNumber()][weaponIndex] |= (CheckInventory(DRLA_UniqueExoticModEffects[weaponIndex][exoticModIndex])) ? ((exoticModIndex == 3) ? 4 : exoticModIndex) : 0;
                }
                else {
                    DRLA_KnownExoticState[PlayerNumber()][weaponIndex] &= ~(bit);
                }

                if (DRLA_KnownExoticState[PlayerNumber()][weaponIndex] != DRLA_OldExoticState[PlayerNumber()][exoticModIndex]) {
                    dowrite = true;
                }
            }
        }
        Delay(2);

    // WRITE --------------------------------------------------
        if (dowrite) {
            // ASSEMBLIES ---
            DRLA_CurrentAssemblerState[PlayerNumber()] = "";

            for (int i = 0; i < DRLA_ASSEMBLYMAX; i++) {
                if (StrLen(DRLA_CurrentAssemblerState[PlayerNumber()]) <= DRLA_ASSEMBLYMAX) {
                    DRLA_CurrentAssemblerState[PlayerNumber()] = StrParam(s:DRLA_CurrentAssemblerState[PlayerNumber()], i:DRLA_AssemblyState[PlayerNumber()][i]);
                }
            }

            SetUserCVarString(PlayerNumber(), "DRLA_knownassemblies", DRLA_CurrentAssemblerState[PlayerNumber()]);

            // UNIQUE+ ---
            DRLA_CurrentExoticModInfo[PlayerNumber()] = "";

            for (int i5 = 0; i5 < DRLA_EXOTICEFFECTS_MAX; i5++) {
                if (StrLen(DRLA_CurrentExoticModInfo[PlayerNumber()]) <= DRLA_EXOTICEFFECTS_MAX) {
                    DRLA_CurrentExoticModInfo[PlayerNumber()] = StrParam(s:DRLA_CurrentExoticModInfo[PlayerNumber()], i:DRLA_KnownExoticState[PlayerNumber()][i5]);
                }
            }

            SetUserCVarString(PlayerNumber(), "DRLA_knownexoticeffects", DRLA_CurrentExoticModInfo[PlayerNumber()]);

            // Sanity delay.
            dowrite = false;
            Delay(15);
        }
    }
}