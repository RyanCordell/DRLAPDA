#define STRING_ARRAY_MAX 1024

#define pulser(a) 0.59 + sin(Timer() * 0.99) * 1.0 / (a)

str stringArray[STRING_ARRAY_MAX];

function void clearStringArray (void) {
    for (int i = 0; i < STRING_ARRAY_MAX; i++) stringArray[i] = "";
}

function int str_find(str haystack, str needle, int index) {
    int i           = index;
    int haystackLen = StrLen(haystack);
    int needleLen   = StrLen(needle);
    int matching    = 0;

    while (i < haystackLen) {
        if (GetChar(haystack, i) == GetChar(needle, 0)) {
            int remaining = needleLen - 1;
            int passed    = 1;

            while (remaining > 0) { 
                if (GetChar(haystack, i + passed) == GetChar(needle, passed)) {
                    passed++;
                    remaining--;
                } else {
                    break;
                }
            }
            return (!remaining) ? i : -1;
        }
        i++;
    }

    return -1;
}

// Thanks to DavidPH!
function int str_split (str separator, str input) {
    int inputLen     = StrLen(input);
    int separatorLen = StrLen(separator);
    int n      = 0;
    int pos    = 0, 
        oldPos = 0;

    clearStringArray();

    while ((pos = str_find(input, separator, oldPos)) != -1) {
        stringArray[n++] = StrMid(input, oldPos, pos - oldPos);
        oldPos = pos + separatorLen;
    }

    stringArray[n++] = StrRight(input, inputLen - oldPos);

    return n+1;
}

function void PDA_PlaySound (str PDA_Sound, str soldier, int volume) {
	if (!GetCVar("DRLA_PDA_soldier"))
		LocalAmbientSound(PDA_Sound,volume);
	else
		LocalAmbientSound(soldier,volume);
}

// Onto other stuff

function void PDA_Wipe (void) {
	for(int wipeIndex = PDA_CURSORID; wipeIndex <= PDA_LASTID; wipeIndex++) {
		HudMessage(s:"\n"; HUDMSG_ALPHA, wipeIndex, CR_UNTRANSLATED, 0, 0, 0, 0);
	}
}

function void PDA_NOPONG (void) {
	for(int wipeIndex = PDA_PONG_LEFTPADDLE; wipeIndex <= PDA_PONG_AISCORE; wipeIndex++) {
		HudMessage(s:"\n"; HUDMSG_PLAIN, wipeIndex, CR_UNTRANSLATED, 0, 0, 0, 0);
	}
}

function void PDA_SetupBounds (void) {
    SetHudSize(newresw,newresh);

    SetHudClipRect(PDA_RectX, PDA_ScrollableY+15, PDA_RectWidth+80, PDA_ScrollableHeight+65, PDA_RectWidth+10);
}

function void PDA_SetCategory (int category) {
   if (PDA_Local.category == category) {
      return;
   }
   
   if (category == PDA_CREDITS) {
      PDA_Local.creditsroll = 120;
   }
   
   if (PDA_Local.category == PDA_PONG) {
      PDA_NOPONG();
      PDA_Local.pongrunning = false;
   }
   
   PDA_Local.texty = 0.0;
   PDA_Local.scrollingy = 20.0;
   PDA_Local.category = category;
}

function void PDA_HasEquipment (void) {
    bool specialArmorTokens = CheckInventory("RLEnclaveSetBonusActive") || CheckInventory("PowerRLPrototypeAssaultShieldProtection") || CheckInventory("PowerRLOverchargeSystemProtection") || CheckInventory("RLCyberneticArmorModdedToken");

    // Normal
    for (int armorIndex = 0; armorIndex < DRLA_ARMORMAX; armorIndex++) {   
        if (CheckInventory(strParam("%s:Token", DRLA_ArmorList[armorIndex][armor_TOKEN]))) {
            if (StrCmp(DRLA_ArmorList[armorIndex][armor_TOKEN], strParam("%s:Boots", StrTrim(DRLA_ArmorList[armorIndex][armor_TOKEN],0,5))) != 0) {
                localArmorIndex = armorIndex;
                localHasArmor = true;
            } else {
                localBootsIndex = armorIndex;
                localHasBoots = true;
            }
        }
        if (!CheckInventory(strParam("%s:Token", DRLA_ArmorList[localArmorIndex][armor_TOKEN]))) {
            localArmorIndex = 0;
            localHasArmor = false;
        }
        if (!CheckInventory(strParam("%s:Token", DRLA_ArmorList[localBootsIndex][armor_TOKEN]))) {
            localBootsIndex = 0;
            localHasBoots = false;
        }
    }

    for (int backpackIndex = 0; backpackIndex < DRLA_BACKPACKMAX; backpackIndex++) {
        if (CheckInventory(DRLA_BackpackList[backpackIndex][0])) {
            localBackpackIndex = backpackIndex;
            localHasBackpack = true;
        } else if (!CheckInventory(DRLA_BackpackList[localBackpackIndex][0])) {
            localHasBackpack = false;
        }
    }
}

/**
 * @name: Create PDA Menu button
 * @param: int id  The ID of the button
 * @param: str label  The button's label that's displayed
 * @param: str graphic  The graphic used
 * @param: float x  Button X co-ordinate
 * @param: float y  Button Y co-ordinate
 * @param: int width  Button width
 * @param: int height  Button height
 */

#define buttonBoundsDefault(a) \
    if (CheckBounds(a)) { \
        (a).state = button_hovered; \
        PDA_LocalMouse.state = mouse_pointer; \
        if ((a).state != (a).oldstate) { \
            PDA_PlaySound("UI/PDA/Select", "pda/soldier", 80); \
        } \
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) { \
            PDA_PlaySound("UI/PDA/ChangePage", "pda/soldier", 80); \
            (a).state = button_clicked; \
        } \
    } else { \
        (a).state = button_none; \
        PDA_Local.playonce = false; \
        if ((a).state != (a).oldstate) { \
            PDA_LocalMouse.state = mouse_default; \
        } \
    } \

#define assignButtonParams(a, b) \
    (a).id = id; \
    (a).x = x; \
    (a).y = y; \
    (a).width = width; \
    (a).height = height; \
    (a).oldstate = (a).state; \
    (a).label = (b); \


function PDAButton PDA_CreateMenuButton (int id, str label, str graphic, float x, float y, int width, int height) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = graphic;

    buttonBoundsDefault(thisButton)

    // Expand upon default bounds check with additional functionality
    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
            PDA_ResetScroll();
        }
    }

    int graphicy = (int)(thisButton.y * newresy) + 0.0;
    int texty    = (int)(thisButton.y * newresy) + 0.0;

    fixed xlabeloff = (strcmp(thisButton.graphic, "PTAB") == 0) ? 4.0 : 0.0;
    fixed ylabeloff = (strcmp(thisButton.graphic, "PTAB") == 0) ? 3.0 : 0.0;

    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    thisButton.graphicX     = (thisButton.x * newresx);
    thisButton.graphicY     = graphicy + 10.0;

    thisButton.textFont  = "PDAFONT";
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID    = (thisButton.id) + 20;
    thisButton.finalX    = (thisButton.x * newresx) + (xlabeloff * newresx);
    thisButton.finalY    = texty + (ylabeloff * newresy);

    return thisButton;
}

function PDAButton PDA_CreateTab (int id, str label, str graphic, float x, float y, int width, int height) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = graphic;
    thisButton.alpha = 0.6;

    buttonBoundsDefault(thisButton)

    fixed xlabeloff = (strcmp(thisButton.graphic, "PTAB") == 0) ? 4.0 : 0.0;
    fixed ylabeloff = (strcmp(thisButton.graphic, "PTAB") == 0) ? 3.0 : 0.0;

    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = thisButton.id;
    thisButton.graphicX = thisButton.x;
    thisButton.graphicY = thisButton.y;

    thisButton.textFont = "PDAFONT";
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID = (thisButton.id) + 20;
    thisButton.finalX = thisButton.x + xlabeloff;
    thisButton.finalY = thisButton.y + ylabeloff;

    return thisButton;
}

function PDAButton PDA_CreateButton (int id, str label, str graphic, float x, float y, int width, int height, float gx, float gy) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = graphic;

    buttonBoundsDefault(thisButton)

    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = thisButton.id;
    thisButton.graphicX = (int)(thisButton.x * newresx) + gx;
    thisButton.graphicY = (int)(thisButton.y * newresy) + gy;

    thisButton.textFont = "PDAFONT";
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID = (thisButton.id) + 20;
    thisButton.finalX = (int)(thisButton.x * newresx) + 4.1;
    thisButton.finalY = (int)(thisButton.y * newresy) + 3.0;

    return thisButton;
}

function PDAButton PDA_CreateAssemblyButton (int id, str graphic, str label, float x, float y, int width, int height, str assembly, str tooltip) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = graphic;
    thisButton.tooltip = tooltip;

    buttonBoundsDefault(thisButton)

    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
            PDA_Local.detailFetch[0] = assembly;
    
            if (strCmp(PDA_Local.detailFetch[0], "") != 0 ) {
                PDA_SetCategory(PDA_ASSEMBLY_DETAIL);
            }
        }

        // Open tooltip
        if  (strCmp(tooltip, "") != 0) {
            SetHudClipRect(PDA_RectX, PDA_ScrollableY+15, PDA_RectWidth+80, PDA_ScrollableHeight+60, PDA_RectWidth+10);
            SetFont("PTIPM");
            HudMessage(s:"A\n"; HUDMSG_ALPHA, PDA_TOOLTIPID, CR_UNTRANSLATED, PDA_RectX + 0.1, (int)(PDA_LocalMouse.y * newresy) + 10.1, 0, 0.9);

            SetFont("STAT_MNO");
            SetHudSize(500, 400);
            SetHudClipRect(PDA_RectX, PDA_ScrollableY+15, PDA_RectWidth+80, PDA_ScrollableHeight+165, PDA_RectWidth+10);
            HudMessage(s:thisButton.tooltip; HUDMSG_ALPHA, PDA_TOOLTIPID - 1, CR_UNTRANSLATED, PDA_RectX + 20.1, (int)(PDA_LocalMouse.y * (400 / oldresh)) + 30.1, 0, thisButton.alpha);

            PDA_SetupBounds();
        }
    }

    int graphicy = (int)(thisButton.y * newresy) + 0.0;
    int texty = (int)(thisButton.y * newresy) + 0.0;

    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = thisButton.id;
    thisButton.graphicX = (int)(thisButton.x * newresx) + 25.1;
    thisButton.graphicY = graphicy + 20.0;
    // if (strCmp(tooltip, "???") == 0) {
    // }
    // else {
    // }

    thisButton.textFont = "STAT_MNO";
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID = thisButton.id - 40;
    thisButton.finalX = (int)(thisButton.x * newresx) + 50.1;
    thisButton.finalY = texty + 4.0;

    return thisButton;
}

function PDAButton PDA_CreateBeastButton (int id, str label, str color, float x, float y, int width, int height, float xoff, float yoff, str beast, str unatcokillphrase) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = "";

    buttonBoundsDefault(thisButton)

    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
            if (strcmp(beast, "") != 0 ) {
                PDA_Local.detailFetch[0] = beast;
                PDA_Local.detailFetch[1] = unatcokillphrase;
        
                PDA_SetCategory(PDA_BESTIARY_DETAIL);
            }
        }
    }

    thisButton.alpha = (thisButton.state == button_hovered) ? 1 : 0.6;
    int graphicy = (int)(thisButton.y * newresy) + 0.0;
    int texty = (int)(thisButton.y * newresy) + 0.0;

    if (debug) {
        SetHudClipRect((int)thisButton.x, (int)thisButton.y, thisButton.width, thisButton.height);
        SetFont("BTNBG");
        HudMessage(s:"A\n"; HUDMSG_ALPHA, (thisButton.id) + 20, CR_UNTRANSLATED, thisButton.x, thisButton.y, 0, thisButton.alpha);
        log("CREATE X %f: Y %f: W %i: H %i:", thisButton.x, thisButton.y, thisButton.width, thisButton.height);
    }
   
    thisButton.graphic = "PLON";
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = thisButton.id;
    thisButton.graphicX = (thisButton.x * newresx) + 5.0;
    thisButton.graphicY = graphicy + 0.1;

    thisButton.textFont = "STAT_MNO";
    thisButton.textFlags = HUDMSG_ALPHA | HUDMSG_COLORSTRING;
    thisButton.textID = (thisButton.id) + 20;
    thisButton.finalX = (thisButton.x * newresx) + 10.0;
    thisButton.finalY = textY + 4.1;

    return thisButton;
}

function PDAButton PDA_CreateHUDButton (int id, str label, str color, float x, float y, int width, int height, float xoff, float yoff, str chosenhud) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = "";

    buttonBoundsDefault(thisButton)

    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
                if (strcmp(chosenhud, "") != 0 ) {
                    // TODO: Don't set category, but instead take and give the necessary tokens
                    // for (int h = 0; h < PDA_LocalHUD, h++) {
                    //   SetInventory(playerSelectedHUD[h], 0)
                    // }
                    // SetInventory(playerSelectedHUD[chosenhud], 1);
                }
        }
    }

    thisButton.alpha = (thisButton.state == button_hovered) ? 1 : 0.6;
    int graphicy = (int)(thisButton.y * newresy) + 0.0;
    int texty = (int)(thisButton.y * newresy) + 0.0;

    if (debug) {
        SetHudClipRect((int)thisButton.x, (int)thisButton.y, thisButton.width, thisButton.height);
        SetFont("BTNBG");
        HudMessage(s:"A\n"; HUDMSG_ALPHA, (thisButton.id) + 20, CR_UNTRANSLATED, thisButton.x, thisButton.y, 0, thisButton.alpha);
        log("CREATE X %f: Y %f: W %i: H %i:", thisButton.x, thisButton.y, thisButton.width, thisButton.height);
    }
   
    thisButton.graphic = "PLON";
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = thisButton.id;
    thisButton.graphicX = (thisButton.x * newresx) + 5.0;
    thisButton.graphicY = graphicY + 0.1;

    thisButton.textFont = "STAT_MNO";
    thisButton.textFlags = HUDMSG_ALPHA | HUDMSG_COLORSTRING;
    thisButton.textID = (thisButton.id) + 20;
    thisButton.finalX = (thisButton.x * newresx) + 10.0;
    thisButton.finalY = textY + 4.1;

    return thisButton;
}

function PDAButton PDA_DisplayArmor (int id, str label, str graphic, float x, float y, int width, int height, str tooltip, fixed opacity) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = graphic;
    thisButton.tooltip = tooltip;

    buttonBoundsDefault(thisButton)

    if (CheckBounds(thisButton) && strCmp(tooltip, "") != 0) {
        SetHudClipRect(PDA_RectX, PDA_ScrollableY+15, PDA_RectWidth+80, PDA_ScrollableHeight+60, PDA_RectWidth+10);

        SetFont("PTIP");
        HudMessage(s:"A\n"; HUDMSG_ALPHA, PDA_TOOLTIPID, CR_UNTRANSLATED, PDA_RectX + 0.1, (int)(PDA_LocalMouse.y * newresy) + 20.1, 0, 0.9);

        SetFont("STAT_MNO");
        HudMessage(s:thisButton.tooltip; HUDMSG_ALPHA, PDA_TOOLTIPID - 1, CR_UNTRANSLATED, PDA_RectX + 10.1, (int)(PDA_LocalMouse.y * newresy) + 30.1, 0, thisButton.alpha);

        PDA_SetupBounds();
    }

    thisButton.alpha = (thisButton.state == button_hovered) ? opacity : opacity - 0.15;
    fixed xlabeloff = 4.0;
    fixed ylabeloff = 3.0;

    int graphicy = (int)(thisButton.y * newresy) + 0.0;
    int texty = (int)(thisButton.y * newresy) + 0.0;

    // SetFont(StrParam("%l:", thisButton.graphic));
    thisButton.graphic = StrParam("%l:", thisButton.graphic); // I don't think this will work the way I think it will.
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = thisButton.id;
    thisButton.graphicX = (int)(thisButton.x * newresx) - 5.1;
    thisButton.graphicY = graphicy + (height / 2) + 3.0;

    thisButton.textFont = "STAT_LWR";
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID = (thisButton.id) + 1;
    thisButton.finalX = (int)(thisButton.x * newresx) + 35.1;
    thisButton.finalY = texty + (height / 2) + 2.0;

    return thisButton;
}

function PDAButton PDA_CreateModPackDisplay (int id, str graphic, str name, float x, float y, int width, int height, fixed alpha) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, name)
    thisButton.graphic = graphic;
    
    buttonBoundsDefault(thisButton)

    int graphicy = (int)(thisButton.y * newresy) + 14.0;

    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID = PDA_MODIDRANGE - thisButton.id;
    thisButton.graphicX = (int)(thisButton.x * newresx) + 15.1;
    thisButton.graphicY = graphicy;

    thisButton.textFont = "STAT_MNO";

    return thisButton;
}

/**
 * @name: Initialize PDA Button
 * @param: PDAButton button
 * @param: int incat
 * @param: int catpage
 * @param: str message
 * @desc: Takes a given PDAButton declaration, then registers events and functionality for it.
 */
function void PDA_InitButton (PDAButton button, int incat, int catpage, str message) {
    if (incat > 0 && PDA_Local.page == catpage) {
        button.state = button_hovered;
        button.alpha = 1;
    }
    
    // TODO: Move buttonBoundsDefault call here, but avoid repeated sound spam and mouse state glitchiness

    switch (button.state) {
        case button_clicked:
            if (!incat) {
                PDA_SetCategory(catpage);
                if(PDA_Local.category != PDA_ASSEMBLY_DETAIL) {
                    PDA_Local.page = 1;
                    PDA_LocalMouse.state = mouse_default;
                }
            }
            if (incat) {
                if(PDA_Local.category != 0 && PDA_Local.page >= 0) {
                    PDA_Local.page = catpage;
                    PDA_Local.scrollingy = 20.0;
                    PDA_LocalMouse.state = mouse_default;
                }
            }
            break;
        case button_hovered:
            if (strcmp(message, "") != 0) {
                SetFont("PDAFONT");
                SetHudClipRect(DES_RECTX, DES_RECTY, DES_RECTWIDTH, DES_RECTHEIGHT, DES_RECTWIDTH);
                HudMessage(s:message; HUDMSG_PLAIN, PDA_CAT_DESCRIPTION, CR_UNTRANSLATED, DES_RECTX + 0.1, DES_RECTY + 0.1, 0);
                PDA_SetupBounds();
            }
            break;
    }

    // Handle the button rendering here
    button.alpha = (button.state == button_hovered) ? 1 : 0.6;
    fixed xlabeloff = (strcmp(button.graphic, "PTAB") == 0) ? 4.0 : 0.0;
    fixed ylabeloff = (strcmp(button.graphic, "PTAB") == 0) ? 3.0 : 0.0;

    SetFont(button.graphic);
    HudMessage(s:"A\n"; button.graphicFlags, button.graphicID, CR_UNTRANSLATED, button.graphicX, button.graphicY, 0, button.alpha);

    SetFont(button.textFont);
    HudMessage(s:button.label; button.textFlags, button.textID, CR_UNTRANSLATED, button.finalX, button.finalY, 0, button.alpha);
}

function int CheckPointerInventory (str item, int pn) {
	SetActivator(0, AAPTR_PLAYER1 << pn);
	return CheckInventory(item);
}

// Debugging functions for SetHudClipRect
function void PrintSprite (str Sprite, int ID, fixed X, fixed Y, fixed DelayTime) {
    SetFont(Sprite);
    HudMessage("A\n"; HUDMSG_PLAIN, ID, CR_UNTRANSLATED, X, Y, DelayTime);
}

function void DrawBorder (int X, int Y, int Width, int Height) {
	SetHudClipRect(X, Y, Width, Height);
	PrintSprite("BarHorz", 0, X, Y, 0.05);
	PrintSprite("BarHorz", 0, X, Y + Height - 1, 0.05);
	PrintSprite("BarVert", 0, X, Y, 0.05);
	PrintSprite("BarVert", 0, X + Width - 1, Y, 0.05);
	SetHudClipRect(0, 0, 0, 0);
	SetFont("");
}

// Useful for scroll arrows
function void DisplayIcon (str Sprite, int ID, fixed X, fixed Y, fixed DelayTime, fixed Alpha) {
    SetFont(Sprite);
    HudMessage(s:"A\n"; HUDMSG_ALPHA, ID, CR_UNTRANSLATED, X, Y, DelayTime, Alpha);
	PDA_DefaultFont();
}

function void DisplayArmorIcon (str Sprite, int ID, fixed X, fixed Y, fixed Alpha) {
    SetFont(StrParam("%l:", Sprite));
    HudMessage(s:"A\n"; HUDMSG_ALPHA, ID, CR_UNTRANSLATED, X, Y, 0, Alpha);
	SetFont("STAT_MNO");
}

function int batoi (str stri)  { 
    int res = 0; 

    for (int i = 0; stri[i] != '\0'; ++i) {
        res = res*10 + stri[i] - '0';
    }

    return res;
} 

function int SetInventory (str Item, int Count) {
    int n = Count - CheckInventory(Item);

    if (n > 0)
        GiveInventory(Item, n);
    else if (n < 0)
        TakeInventory(Item, -n);

    return n;
}

// Setup scrolling arrows for pages with lots of text 
function void PDA_SetScrollArrows (int scrollDownMax) {
	if(PDA_Local.scrollingy >= 0)
		DisplayIcon("RLZARU", PDA_SCROLLUPID, PDA_RectWidth + 0.1, PDA_RectHeight + 0.1, 0, 0.0);
	else
		DisplayIcon("RLZARU", PDA_SCROLLUPID, PDA_RectWidth + 0.1, PDA_RectY + 10.1, 0, 0.5);


	if(PDA_Local.scrollingy <= scrollDownMax)
		DisplayIcon("RLZARD", PDA_SCROLLDNID, PDA_RectWidth + 0.1, PDA_RectHeight + 0.1, 0, 0.0);
	else
		DisplayIcon("RLZARD", PDA_SCROLLDNID, PDA_RectWidth + 0.1, PDA_RectHeight + 0.1, 0, 0.5);
}

function void PDA_SetScrollArrows2 (int scrollDownMax, float x, float y) {
    bool notbot = PDA_Local.scrollingy > scrollDownMax;
    bool nottop = PDA_Local.scrollingy < 0;

    if(notbot) DisplayIcon("RLZARD", PDA_SCROLLDNID, x, y, 0, 0.5);
    if(nottop) DisplayIcon("RLZARU", PDA_SCROLLUPID, x, PDA_RectY + 40.1, 0, 0.5);
}

// THEN ERASE THEM FROM REALITY
function void PDA_UnsetScrollArrows (void) {
	DisplayIcon("RLZARU", PDA_SCROLLUPID, PDA_RectWidth + 0.1, PDA_RectY + 10.1, 0, 0);
	DisplayIcon("RLZARD", PDA_SCROLLDNID, PDA_RectWidth + 0.1, PDA_RectHeight + 0.1, 0, 0);
}

function void PDA_SetScrollWindow (int scrollDownMax) {
	SetHudClipRect(PDA_RectX, PDA_ScrollableY, PDA_RectWidth, PDA_ScrollableHeight, PDA_RectWidth);
	PDA_SetScrollArrows2(scrollDownMax, PDA_RectWidth + 0.1, PDA_RectHeight + 0.1);
}

function void PDA_UnsetScrollWindow (void) {
	SetHudClipRect(PDA_RectX, PDA_RectY, PDA_RectWidth, PDA_RectHeight, PDA_RectWidth);
}

// Instead of clogging up the script, just do it here.
function void PDA_EstablishModLimits (void) {
	if (CheckInventory("RLStandardWeaponToken") || CheckInventory("RLExoticWeaponToken")) {
		PDA_Local.maxmodlimit = 4;
  }

	if (CheckInventory("RLSuperiorWeaponToken") || CheckInventory("RLBasicAssemblyTypeWeapon")) {
		PDA_Local.maxmodlimit = 2;
  }

	if (CheckInventory("RLUniqueWeaponToken") || CheckInventory("RLDemonicWeaponToken") || CheckInventory("RLLegendaryWeaponToken") || CheckInventory("RLAdvancedAssemblyTypeWeapon")) {
		if (StrCmp(GetWeapon(),"RLDuke2Rifle") == 0 || StrCmp(GetWeapon(),"RLChameleonRifle") == 0 || StrCmp(GetWeapon(),"RLWidowmakerSMG") == 0)
			PDA_Local.maxmodlimit = 3;
		else
			PDA_Local.maxmodlimit = 1;
  }

	if (CheckInventory("RLMasterAssemblyTypeWeapon") || CheckInventory("RLSuperiorAssembledWeaponToken")) {
		PDA_Local.maxmodlimit = 0;
  }
}

// kd: An "odd sensitivity cvar" is a mouse sensitivity cvar that became unavailable starting gzd
// 4.5.0 when mouse sensitivity stuff got overhauled. One example is mouse_sensitivity being removed
// and m_sensitivity_x and m_sensitivity_y being added instead.
function fixed GetOddSensitivityCVarFixed (str cvar_name, fixed default_value) {
    fixed cvar_value = GetCVarFixed(cvar_name);
    
    if(cvar_value == 0) {
        return default_value;
    }
    
    return cvar_value;
}

function fixed max (fixed x, fixed y) {
    if(x < y) {
        return y;
    }
    
    return x;
}
