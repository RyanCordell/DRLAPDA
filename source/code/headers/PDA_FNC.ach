#define STRING_ARRAY_MAX 1024
str stringArray[STRING_ARRAY_MAX];


#define pulser(a, b) (b ? b : 0.59) + sin(Timer() * 0.99) / (a)

function void clearStringArray (void) {
    for (int i = 0; i < STRING_ARRAY_MAX; i++) stringArray[i] = "";
}

function int str_find(str haystack, str needle, int index) {
    int i           = index;
    int haystackLen = StrLen(haystack);
    int needleLen   = StrLen(needle);
    int matching    = 0;

    while (i < haystackLen) {
        if (GetChar(haystack, i) == GetChar(needle, 0)) {
            int remaining = needleLen - 1;
            int passed    = 1;

            while (remaining > 0) {
                if (GetChar(haystack, i + passed) == GetChar(needle, passed)) {
                    passed++;
                    remaining--;
                } else {
                    break;
                }
            }
            return (!remaining) ? i : -1;
        }
        i++;
    }

    return -1;
}

// Thanks to DavidPH!
function int str_split (str separator, str input) {
    int inputLen     = StrLen(input);
    int separatorLen = StrLen(separator);
    int n      = 0;
    int pos    = 0,
        oldPos = 0;

    clearStringArray();

    while ((pos = str_find(input, separator, oldPos)) != -1) {
        stringArray[n++] = StrMid(input, oldPos, pos - oldPos);
        oldPos = pos + separatorLen;
    }

    stringArray[n++] = StrRight(input, inputLen - oldPos);

    return n+1;
}

// Debugging functions for SetHudClipRect
function void PrintSprite (str Sprite, int ID, fixed X, fixed Y, fixed DelayTime) {
    SetFont(Sprite);
    HudMessage("A\n"; HUDMSG_PLAIN, ID, CR_UNTRANSLATED, X, Y, DelayTime);
}

function void PrintSpriteAlpha (str Sprite, int ID, fixed X, fixed Y, fixed DelayTime, fixed Alpha) {
    SetFont(Sprite);
    HudMessage("A\n"; HUDMSG_ALPHA, ID, CR_UNTRANSLATED, X, Y, DelayTime, Alpha);
}

function void DrawBorder (int X, int Y, int Width, int Height) {
	SetHudClipRect(X, Y, Width, Height);
	PrintSpriteAlpha("BarHorz", 0, X,             Y,              0.05, 0.3);
	PrintSpriteAlpha("BarVert", 0, X,             Y,              0.05, 0.3);
	PrintSpriteAlpha("BarVert", 0, X + Width - 1, Y,              0.05, 0.3);
	PrintSpriteAlpha("BarHorz", 0, X,             Y + Height - 1, 0.05, 0.3);
    // log("border (x: %d:, y: %d:, w: %d:, h: %d:)", X, Y, X + Width - 1, Y + Height - 1);
	// SetHudClipRect(0, 0, 0, 0);
	SetFont("");
}

// Useful for scroll arrows
function void DisplayIcon (str Sprite, int ID, fixed X, fixed Y, fixed DelayTime, fixed Alpha) {
    SetFont(Sprite);
    HudMessage(s:"A\n"; HUDMSG_ALPHA, ID, CR_UNTRANSLATED, X, Y, DelayTime, Alpha);
	PDA_DefaultFont();
}
function void DisplayIconParallax (str Sprite, int ID, fixed X, fixed Y, fixed DelayTime, fixed Alpha) {
    SetFont(Sprite);
    int _yTemp = PDA_Local.scrollingy * NEW_RESOLUTION_Y + Y;
    HudMessage(s:"A\n"; HUDMSG_ALPHA, ID, CR_UNTRANSLATED, X, _yTemp + Y_TOP_EDGE, DelayTime, Alpha);
	PDA_DefaultFont();
}

function void DisplayArmorIcon (str Sprite, int ID, fixed X, fixed Y, fixed Alpha) {
    SetFont(StrParam("%l:", Sprite));
    HudMessage(s:"A\n"; HUDMSG_ALPHA, ID, CR_UNTRANSLATED, X, Y, 0, Alpha);
	SetFont(USED_PDA_FONT);
}

function int batoi (str stri)  {
    int res = 0;

    for (int i = 0; stri[i] != '\0'; ++i) {
        res = res*10 + stri[i] - '0';
    }

    return res;
}

// kd: An "odd sensitivity cvar" is a mouse sensitivity cvar that became unavailable starting gzd
// 4.5.0 when mouse sensitivity stuff got overhauled. One example is mouse_sensitivity being removed
// and m_sensitivity_x and m_sensitivity_y being added instead.
function fixed GetOddSensitivityCVarFixed (str cvar_name, fixed default_value) {
    fixed cvar_value = GetCVarFixed(cvar_name);

    if(cvar_value == 0) {
        return default_value;
    }

    return cvar_value;
}

function fixed max (fixed x, fixed y) {
    return (x < y) ? y : x;
}

function void HudMessageParallax(str text, int type, int id, int color, fixed x, fixed y, fixed holdTime, fixed alpha) {
    int _xTemp = (PDA_SCREEN_X + x) * NEW_RESOLUTION_X;
    int _yTemp = (PDA_Local.scrollingy * NEW_RESOLUTION_Y) + y;
    fixed _x = _xTemp + X_LEFT_EDGE;
    fixed _y = _yTemp + Y_TOP_EDGE;
    HudMessage(s:text; type, id, color, _x, _y, holdTime, alpha);

    if (debug) log("text (x: %f:, y: %f:)", _x, _y);
}

function int CheckPointerInventory (str item, int pn) {
	SetActivator(0, AAPTR_PLAYER1 << pn);
	return CheckInventory(item);
}

function int SetInventory (str Item, int Count) {
    int n = Count - CheckInventory(Item);

    if (n > 0)
        GiveInventory(Item, n);
    else if (n < 0)
        TakeInventory(Item, -n);

    return n;
}

// int _xTemp = (PDA_SCREEN_X * NEW_RESOLUTION_X) + (x * NEW_RESOLUTION_X);
#define assignButtonParams(a, b) \
    int _xTemp = PDA_SCREEN_X + x; \
    int _yTemp = PDA_Local.scrollingy + y; \
    (a).id = id; \
    (a).x = _xTemp + X_LEFT_EDGE; \
    (a).y = _yTemp + Y_TOP_EDGE; \
    (a).width = width; \
    (a).height = height; \
    (a).oldstate = (a).state; \
    (a).label = (b); \

#define CheckBounds(a) \
    PDA_LocalMouse.x >= (a).x - 1 && PDA_LocalMouse.x <= (a).x + (a).width \
    && \
    PDA_LocalMouse.y >= (a).y - 2 && PDA_LocalMouse.y <= (a).y + (a).height \

#define buttonBoundsDefault(a) \
    if (CheckBounds(a)) { \
        (a).state = button_hovered; \
        PDA_LocalMouse.state = mouse_pointer; \
        if ((a).state != (a).oldstate) { \
            PDA_PlaySound("UI/PDA/Select", "pda/soldier", 80); \
        } \
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) { \
            PDA_PlaySound("UI/PDA/ChangePage", "pda/soldier", 80); \
            (a).state = button_clicked; \
        } \
    } else { \
        (a).state = button_none; \
        PDA_Local.playonce = false; \
        if ((a).state != (a).oldstate) { \
            PDA_LocalMouse.state = mouse_default; \
        } \
    } \
    if (debug) log("btn-col (x: %f:, y: %f:, w: %d:, h: %d:)", (a).x, (a).y, (a).width, (a).height); \

// Onto other stuff

function void PDA_PlaySound (str PDA_Sound, str soldier, int volume) {
    LocalAmbientSound(!GetCVar("DRLA_PDA_soldier") ? PDA_Sound : soldier, volume);
}

function void PDA_Wipe (void) {
	for(int wipeIndex = PDA_CURSORID; wipeIndex <= PDA_LASTID; wipeIndex++) {
		HudMessage(s:"\n"; HUDMSG_ALPHA, wipeIndex, CR_UNTRANSLATED, 0, 0, 0, 0);
	}
}

function void PDA_NOPONG (void) {
	for(int wipeIndex = PDA_PONG_LEFTPADDLE; wipeIndex <= PDA_PONG_AISCORE; wipeIndex++) {
		HudMessage(s:"\n"; HUDMSG_PLAIN, wipeIndex, CR_UNTRANSLATED, 0, 0, 0, 0);
	}
}

function void PDA_SetupHeaderBounds (void) {
    SetHudSize(NEW_RESOLUTION_WIDTH, NEW_RESOLUTION_HEIGHT);

    int x = PDA_SCREEN_X + 4;
    int y = PDA_SCREEN_Y;
    int w = PDA_SCREEN_WIDTH * NEW_RESOLUTION_X;
    int h = PDA_SCREEN_Y + 38;
    int ww = (PDA_SCREEN_WIDTH * NEW_RESOLUTION_X) - PDA_SCREEN_X;

    SetHudClipRect(x, y, w, h, ww);
}

function void PDA_SetupScreenBounds (void) {
    SetHudSize(NEW_RESOLUTION_WIDTH, NEW_RESOLUTION_HEIGHT);

    int x = PDA_SCREEN_X + 4;
    int y = PDA_SCROLLABLE_Y;
    int w = PDA_SCREEN_WIDTH * NEW_RESOLUTION_X;
    int h = PDA_SCROLLABLE_HEIGHT;
    int ww = (PDA_SCREEN_WIDTH * NEW_RESOLUTION_X) - PDA_SCREEN_X;

    SetHudClipRect(x, y, w, h, ww);

    if (debug) {
        DrawBorder(PDA_SCREEN_X * NEW_RESOLUTION_X, PDA_SCROLLABLE_Y,
                PDA_SCREEN_WIDTH * NEW_RESOLUTION_X, PDA_SCROLLABLE_HEIGHT);
        SetHudClipRect(x, y, w, h, ww);
        // log("cliprect (x: %d:, y: %d:, w: %d:, h: %d:)", x, y, w, h);
    }
}

function void PDA_SetupFloatingTooltip (str graphic, str tooltip) {
    SetFont(graphic);
    int _xTemp = (PDA_SCREEN_X + 20) * NEW_RESOLUTION_X;
    int _yTemp = PDA_LocalMouse.y * NEW_RESOLUTION_Y + 20;

    HudMessage(s:"A\n"; HUDMSG_ALPHA, PDA_TOOLTIPID, CR_UNTRANSLATED,
        _xTemp + X_LEFT_EDGE, _yTemp + Y_TOP_EDGE,
        0, 0.9);

    SetFont(USED_PDA_FONT);

    SetHudClipRect(
        PDA_SCREEN_X + 4, PDA_SCROLLABLE_Y,
        PDA_SCREEN_WIDTH * NEW_RESOLUTION_X, PDA_SCROLLABLE_HEIGHT,
        PDA_FLOATING_TOOLTIP_WRAP
    );

    _yTemp = PDA_LocalMouse.y * NEW_RESOLUTION_Y;

    const float xOffset = 20.0;
    const float yOffset = 30.0;

    float _ttX = _xTemp + xOffset + X_LEFT_EDGE;
    float _ttY = _yTemp + yOffset + Y_TOP_EDGE;

    HudMessage("%s:", tooltip; HUDMSG_ALPHA, PDA_TOOLTIPID - 1, CR_UNTRANSLATED,
    _ttX, _ttY, 0.0, 1.0);
}

function void PDA_SetupPageTooltip (str graphic, str tooltip, float alpha) {
    SetHudClipRect(
        PDA_FLOATING_TOOLTIP_GRAPHIC_X, PDA_FLOATING_TOOLTIP_GRAPHIC_Y,
        PDA_FLOATING_TOOLTIP_GRAPHIC_WIDTH, PDA_FLOATING_TOOLTIP_GRAPHIC_HEIGHT,
        PDA_FLOATING_TOOLTIP_GRAPHIC_WRAP
    );

    SetFont(graphic);
    HudMessage(s:"A\n"; HUDMSG_ALPHA, PDA_TOOLTIPID, CR_UNTRANSLATED,
        PDA_SCREEN_X + X_LEFT_EDGE, (int)(PDA_LocalMouse.y * NEW_RESOLUTION_Y) + 20.0 + Y_TOP_EDGE,
        0, 0.9);

    SetFont(USED_PDA_FONT);
    HudMessage(s:tooltip; HUDMSG_ALPHA, PDA_TOOLTIPID - 1, CR_UNTRANSLATED,
    10.0,
    (int)(PDA_LocalMouse.y * NEW_RESOLUTION_Y) + 30.0,
    0, alpha);

    PDA_SetupScreenBounds();
}

function void PDA_SetCategory (int category) {
   if (PDA_Local.category == category) {
      return;
   }

   if (category == PDA_CREDITS) {
      PDA_Local.creditsroll = 120;
   }

   if (PDA_Local.category == PDA_PONG) {
      PDA_NOPONG();
      PDA_Local.pongrunning = false;
   }

   PDA_Local.texty = 0.0;
   PDA_Local.scrollingy = PDA_SCREEN_Y;
   PDA_Local.category = category;
}

function void PDA_HasEquipment (void) {
    bool specialArmorTokens = CheckInventory("RLEnclaveSetBonusActive") || CheckInventory("PowerRLPrototypeAssaultShieldProtection") || CheckInventory("PowerRLOverchargeSystemProtection") || CheckInventory("RLCyberneticArmorModdedToken");

    // Normal
    for (int armorIndex = 0; armorIndex < DRLA_ARMORMAX; armorIndex++) {
        if (CheckInventory(strParam("%s:Token", DRLA_ArmorList[armorIndex][armor_TOKEN]))) {
            if (StrCmp(DRLA_ArmorList[armorIndex][armor_TOKEN], strParam("%s:Boots", StrTrim(DRLA_ArmorList[armorIndex][armor_TOKEN],0,5))) != 0) {
                localArmorIndex = armorIndex;
                localHasArmor = true;
            } else {
                localBootsIndex = armorIndex;
                localHasBoots = true;
            }
        }
        if (!CheckInventory(strParam("%s:Token", DRLA_ArmorList[localArmorIndex][armor_TOKEN]))) {
            localArmorIndex = 0;
            localHasArmor = false;
        }
        if (!CheckInventory(strParam("%s:Token", DRLA_ArmorList[localBootsIndex][armor_TOKEN]))) {
            localBootsIndex = 0;
            localHasBoots = false;
        }
    }

    for (int backpackIndex = 0; backpackIndex < DRLA_BACKPACKMAX; backpackIndex++) {
        if (CheckInventory(DRLA_BackpackList[backpackIndex][0])) {
            localBackpackIndex = backpackIndex;
            localHasBackpack = true;
        } else if (!CheckInventory(DRLA_BackpackList[localBackpackIndex][0])) {
            localHasBackpack = false;
        }
    }
}

/**
 * @name: Create PDA Menu button
 * @param: int id  The ID of the button
 * @param: str label  The button's label that's displayed
 * @param: str graphic  The graphic used
 * @param: float x  Button X co-ordinate
 * @param: float y  Button Y co-ordinate
 * @param: int width  Button width
 * @param: int height  Button height
 */

// !! When debugging button hitboxes via DrawBorder, remember to multiply X, Y, W, H with matching NEW_RESOLUTION_ variables !!

function PDAButton PDA_CreateMenuButton (int id, str label, str graphic, float x, float y, int width, int height) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    buttonBoundsDefault(thisButton)

    // Expand upon default bounds check with additional functionality
    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
            PDA_ResetScroll();
        }
    }

    int graphicy = (int)(thisButton.y * NEW_RESOLUTION_Y) + 0.0;
    int texty    = (int)(thisButton.y * NEW_RESOLUTION_Y) + 0.0;

    fixed xlabeloff = (strcmp(thisButton.graphic, "PTAB") == 0) ? 4.0 : 0.0;
    fixed ylabeloff = (strcmp(thisButton.graphic, "PTAB") == 0) ? 3.0 : 0.0;

    thisButton.graphic      = graphic;
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    thisButton.graphicX     = (thisButton.x * NEW_RESOLUTION_X);
    thisButton.graphicY     = graphicy + 10.0;

    thisButton.textFont     = PDA_FONT_SLIM;
    thisButton.textFlags    = HUDMSG_ALPHA;
    thisButton.textID       = thisButton.graphicID + 20;
    thisButton.finalX       = thisButton.graphicX + (xlabeloff * NEW_RESOLUTION_X);
    thisButton.finalY       = texty + (ylabeloff * NEW_RESOLUTION_Y);

    if (debug) {
        DrawBorder(thisButton.x * NEW_RESOLUTION_X, thisButton.y * NEW_RESOLUTION_Y, width * NEW_RESOLUTION_X, height * NEW_RESOLUTION_Y);
        PDA_SetupScreenBounds();
    }
    // log("gx: %d: | gw: %d: | fx: %d:", thisButton.graphicX, gw, thisButton.finalX);
    return thisButton;
}

function PDAButton PDA_CreateTab (int id, str label, str graphic, float x, float y, int width, int height) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    buttonBoundsDefault(thisButton)

    thisButton.y       = PDA_SCREEN_Y + y + Y_TOP_EDGE; // Override as this isn't meant to scroll
    thisButton.alpha   = 0.6;

    fixed xlabeloff = (strcmp(graphic, "PTAB") == 0) ? 6.0 : 0.0;
    fixed ylabeloff = (strcmp(graphic, "PTAB") == 0) ? 7.0 : 0.0;

    thisButton.graphic      = graphic;
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    int _gxTemp             = thisButton.x * NEW_RESOLUTION_X;
    thisButton.graphicX     = _gxTemp + X_LEFT_EDGE;
    int _gyTemp             = thisButton.y * NEW_RESOLUTION_Y;
    thisButton.graphicY     = _gyTemp + Y_TOP_EDGE;
    thisButton.type         = "tab";

    thisButton.textFont     = PDA_FONT_SLIM;
    thisButton.textFlags    = HUDMSG_ALPHA;
    thisButton.textID       = thisButton.graphicID + 10;
    thisButton.finalX       = (int)thisButton.graphicX + xlabeloff;
    thisButton.finalY       = (int)thisButton.graphicY + ylabeloff;

    if (debug) {
        int x = thisButton.x * NEW_RESOLUTION_X,
            y = thisButton.y * NEW_RESOLUTION_Y,
            w = width        * NEW_RESOLUTION_X,
            h = height       * NEW_RESOLUTION_Y;
        log("tab (x: %f:, y: %f:, w: %d:, h: %d:)", x, y, w, h);
        DrawBorder(x, y, w, h);
    }

    return thisButton;
}

function PDAButton PDA_CreateButton (int id, str label, str graphic, float x, float y, int width, int height, float gw, float gh) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    buttonBoundsDefault(thisButton)

    const float X_MARGIN = 1.0;

    thisButton.graphic      = graphic;
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    int _gxTemp             = thisButton.x * NEW_RESOLUTION_X;
    thisButton.graphicX     = _gxTemp + X_LEFT_EDGE;
    int _gyTemp             = thisButton.y * NEW_RESOLUTION_Y;
    thisButton.graphicY     = PDA_SCREEN_Y + _gyTemp - (int)(gh / 2);
    thisButton.type         = "generic";

    thisButton.textFont     = PDA_FONT_SLIM;
    thisButton.textFlags    = HUDMSG_ALPHA;
    thisButton.textID       = thisButton.graphicID + 20;
    int _fxTemp             = (int)thisButton.graphicX + (gw + X_MARGIN) * NEW_RESOLUTION_X;
    thisButton.finalX       = _fxTemp + X_LEFT_EDGE;
    thisButton.finalY       = thisButton.graphicY;

    if (debug) {
        int x = thisButton.x * NEW_RESOLUTION_X,
            y = thisButton.y * NEW_RESOLUTION_Y,
            w = width        * NEW_RESOLUTION_X,
            h = height       * NEW_RESOLUTION_Y;
        log("btn-gfx (x: %f:, y: %f:, w: %d:, h: %d:)", x, y, w, h);
        DrawBorder(x, y, w, h);
        PDA_SetupScreenBounds();
    }
    // log("gx: %d: | gw: %d: | fx: %d:", thisButton.graphicX, gw, thisButton.finalX);
    return thisButton;
}

function PDAButton PDA_CreateAssemblyButton (int id, str graphic, str label, float x, float y, int width, int height, str assembly, str tooltip) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    buttonBoundsDefault(thisButton)


    const int X_MARGIN = 12.0;

    thisButton.graphic      = graphic;
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    int _gxTemp             = (thisButton.x) * NEW_RESOLUTION_X;
    int _gyTemp             = (int)(thisButton.y * NEW_RESOLUTION_Y) + 0.0;
    thisButton.graphicX     = _gxTemp + X_LEFT_EDGE;
    thisButton.graphicY     = _gyTemp + 20.0;
    thisButton.type         = "assembly";

    thisButton.textFont  = USED_PDA_FONT;
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID    = thisButton.id - 40;
    int _fxTemp          = (int)thisButton.graphicX * NEW_RESOLUTION_X;
    int _fyTemp          = (int)(thisButton.y * NEW_RESOLUTION_Y) + 0.0;
    thisButton.finalX    = _fxTemp + X_MARGIN + X_LEFT_EDGE;
    thisButton.finalY    = _fyTemp + 4.0;

    thisButton.tooltip = tooltip;
    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
            PDA_Local.detailFetch[0] = assembly;

            if (strCmp(PDA_Local.detailFetch[0], "") != 0 ) {
                PDA_SetCategory(PDA_ASSEMBLY_DETAIL);
            }
        }

        // Open tooltip
        if (strCmp(thisButton.tooltip, "") != 0 && strCmp(assembly, "") != 0) {
            PDA_SetupFloatingTooltip("PTIPM", thisButton.tooltip);
            PDA_SetupScreenBounds();
        }
    }

    if (debug) {
        DrawBorder(thisButton.x * NEW_RESOLUTION_X, thisButton.y * NEW_RESOLUTION_Y, width * NEW_RESOLUTION_X, height * NEW_RESOLUTION_Y);
        PDA_SetupScreenBounds();
    }
    return thisButton;
}

function PDAButton PDA_CreateBeastButton (int id, str label, str color, float x, float y, int width, int height, float xoff, float yoff, str beast, str unatcokillphrase) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    thisButton.graphic = "";

    buttonBoundsDefault(thisButton)

    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
            if (strcmp(beast, "") != 0 ) {
                PDA_Local.detailFetch[0] = beast;
                PDA_Local.detailFetch[1] = unatcokillphrase;

                PDA_SetCategory(PDA_BESTIARY_DETAIL);
            }
        }
    }

    thisButton.alpha = (thisButton.state == button_hovered) ? 1 : 0.6;

    thisButton.graphic      = "PLON";
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    thisButton.graphicX     = (thisButton.x * NEW_RESOLUTION_X) + 5.0;
    int graphicy     = (int)(thisButton.y * NEW_RESOLUTION_Y) + 0.0;
    thisButton.graphicY     = graphicy + X_LEFT_EDGE;
    thisButton.type         = "beast";

    thisButton.textFont  = USED_PDA_FONT;
    thisButton.textFlags = HUDMSG_ALPHA | HUDMSG_COLORSTRING;
    thisButton.textID    = (thisButton.id) + 20;
    thisButton.finalX    = (thisButton.x * NEW_RESOLUTION_X) + 10.0;
    int texty        = (int)(thisButton.y * NEW_RESOLUTION_Y) + 0.0;
    thisButton.finalY    = textY + 4.0 + X_LEFT_EDGE;

    if (debug) {
        DrawBorder(thisButton.x * NEW_RESOLUTION_X, thisButton.y * NEW_RESOLUTION_Y, width * NEW_RESOLUTION_X, height * NEW_RESOLUTION_Y);
        PDA_SetupScreenBounds();
    }
    return thisButton;
}

function PDAButton PDA_CreateHUDButton (int id, str label, str color, float x, float y, int width, int height, float xoff, float yoff, str chosenhud) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    buttonBoundsDefault(thisButton)

    if (CheckBounds(thisButton)) {
        if (PDA_LocalMouse.m1 && !(PDA_LocalMouse.oldState & BT_ATTACK)) {
                if (strcmp(chosenhud, "") != 0 ) {
                    // TODO: Don't set category, but instead take and give the necessary tokens
                    // for (int h = 0; h < PDA_LocalHUD, h++) {
                    //   SetInventory(playerSelectedHUD[h], 0)
                    // }
                    // SetInventory(playerSelectedHUD[chosenhud], 1);
                }
        }
    }

    thisButton.alpha = (thisButton.state == button_hovered) ? 1 : 0.6;

    thisButton.graphic      = "PLON";
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    thisButton.graphicX     = (thisButton.x * NEW_RESOLUTION_X) + 5.0;
    int _gyTemp             = thisButton.y * NEW_RESOLUTION_Y;
    thisButton.graphicY     = _gyTemp + X_LEFT_EDGE;
    thisButton.type         = "hud";

    thisButton.textFont  = USED_PDA_FONT;
    thisButton.textFlags = HUDMSG_ALPHA | HUDMSG_COLORSTRING;
    thisButton.textID    = (thisButton.id) + 20;
    thisButton.finalX    = (thisButton.x * NEW_RESOLUTION_X) + 10.0;
    int _tyTemp          = thisButton.y * NEW_RESOLUTION_Y;
    thisButton.finalY    = _tyTemp + 4.0 + X_LEFT_EDGE;

    if (debug) {
        DrawBorder(thisButton.x * NEW_RESOLUTION_X, thisButton.y * NEW_RESOLUTION_Y, width * NEW_RESOLUTION_X, height * NEW_RESOLUTION_Y);
        PDA_SetupScreenBounds();
    }
    return thisButton;
}

function PDAButton PDA_DisplayArmor (int id, str label, str graphic, float x, float y, int width, int height, str tooltip, fixed opacity) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, label)
    buttonBoundsDefault(thisButton)

    // thisButton.alpha = (thisButton.state == button_hovered) ? opacity : opacity - 0.15;
    thisButton.alpha = opacity;

    // SetFont(StrParam("%l:", thisButton.graphic));
    thisButton.graphic      = StrParam("%l:", graphic); // I don't think this will work the way I think it will.
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = thisButton.id;
    int _gfxTemp = thisButton.x * NEW_RESOLUTION_X;
    int _gfyTemp = thisButton.y * NEW_RESOLUTION_Y + (height / 2);
    thisButton.graphicX     = _gfxTemp - 5.0 + X_LEFT_EDGE;
    thisButton.graphicY     = _gfyTemp + 3.0;
    thisButton.type         = "armor";

    thisButton.textFont  = PDA_FONT_LOWER;
    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textID    = (thisButton.id) + 1;
    int _fxTemp          = thisButton.x * NEW_RESOLUTION_X;
    int _fyTemp          = thisButton.y * NEW_RESOLUTION_Y;
    thisButton.finalX    = _fxTemp + 35.0 + X_LEFT_EDGE;
    thisButton.finalY    = _fyTemp + (height / 2) + 2.0;

    thisButton.tooltip = tooltip;

    if (CheckBounds(thisButton) && strCmp(tooltip, "") != 0) {
        // PDA_SetupPageTooltip("PTIP", thisButton.tooltip, thisButton.alpha);
    }

    if (debug) {
        DrawBorder(thisButton.x * NEW_RESOLUTION_X, thisButton.y * NEW_RESOLUTION_Y, width * NEW_RESOLUTION_X, height * NEW_RESOLUTION_Y);
        PDA_SetupScreenBounds();
    }
    return thisButton;
}

function PDAButton PDA_CreateModPackDisplay (int id, str graphic, float x, float y, int width, int height, fixed alpha) {
    #define thisButton PDA_LocalButton[id]
    assignButtonParams(thisButton, '')

    thisButton.graphic      = graphic;
    thisButton.graphicFlags = HUDMSG_ALPHA;
    thisButton.graphicID    = PDA_ASSEMBLYIDRANGE + PDA_MODMAX + thisButton.id;
    thisButton.graphicX     = (int)(thisButton.x * NEW_RESOLUTION_X) + 15.0 + X_LEFT_EDGE;
    thisButton.graphicY     = 0.0;
    thisButton.type         = "modpack";

    thisButton.textFlags = HUDMSG_ALPHA;
    thisButton.textFont  = USED_PDA_FONT;
    thisButton.finalX    = PDA_SCREEN_X + 35.0 + X_LEFT_EDGE;

    if (debug) {
        DrawBorder(thisButton.x * NEW_RESOLUTION_X, thisButton.y * NEW_RESOLUTION_Y, width * NEW_RESOLUTION_X, height * NEW_RESOLUTION_Y);
        PDA_SetupScreenBounds();
    }
    return thisButton;
}

/**
 * @name: Initialize PDA Button
 * @param: PDAButton button
 * @param: int incat
 * @param: int catpage
 * @param: str message
 * @desc: Takes a given PDAButton declaration, then registers events and functionality for it.
 */
function void PDA_InitButton (PDAButton button, int incat, int catpage, str message, int onlyVisual) {
    if (button.type != "tab") {
        PDA_SetupScreenBounds();
    } else {
        PDA_SetupHeaderBounds();
    }

    if (incat > 0 && PDA_Local.page == catpage) {
        button.state = button_hovered;
        button.alpha = 1;
    }

    // TODO: Move buttonBoundsDefault call here, but avoid repeated sound spam and mouse state glitchiness

    if (!onlyVisual) {
        switch (button.state) {
            case button_clicked:
                if (!incat) {
                    PDA_SetCategory(catpage);
                    if(PDA_Local.category != PDA_ASSEMBLY_DETAIL) {
                        PDA_Local.page = 1;
                        PDA_LocalMouse.state = mouse_default;
                    }
                }
                if (incat) {
                    if(PDA_Local.category != 0 && PDA_Local.page >= 0) {
                        PDA_Local.page = catpage;
                        PDA_Local.scrollingy = 20.0;
                        PDA_LocalMouse.state = mouse_default;
                    }
                }
                break;
            case button_hovered:
                if (strcmp(message, "") != 0) {
                    SetFont(PDA_FONT_SLIM);
                    SetHudClipRect(
                        PDA_MENU_TOOLTIP_X,     PDA_MENU_TOOLTIP_Y,
                        PDA_MENU_TOOLTIP_WIDTH, PDA_MENU_TOOLTIP_HEIGHT,
                        PDA_MENU_TOOLTIP_WIDTH
                    );
                    HudMessage(s:message; HUDMSG_PLAIN, PDA_CAT_DESCRIPTION, CR_UNTRANSLATED,
                    PDA_MENU_TOOLTIP_X + X_LEFT_EDGE, PDA_MENU_TOOLTIP_Y + Y_TOP_EDGE, 0);
                    PDA_SetupScreenBounds();
                }
                break;
        }
    }

    // Handle the button rendering here
    if (button.type == "assembly") {
        if (button.tooltip != "???") {
            button.alpha = (button.state == button_hovered) ? pulser(2.0, 0.8) : 0.6;
        } else {
            button.alpha = 1.0;
        }
    } else {
        if (button.type != "tab" && button.type != "armor") {
            button.alpha = (button.state == button_hovered) ? pulser(2.0, 0.8) : 0.6;
        }
    }
    fixed xlabeloff = (strcmp(button.graphic, "PTAB") == 0) ? 4.0 : 0.0;
    fixed ylabeloff = (strcmp(button.graphic, "PTAB") == 0) ? 3.0 : 0.0;

    PrintSpriteAlpha(button.graphic, button.graphicID, button.graphicX, button.graphicY, 0, button.alpha);

    SetFont(button.textFont);
    HudMessage(s:button.label; button.textFlags, button.textID, CR_UNTRANSLATED, button.finalX, button.finalY, 0, button.alpha);
}

// This function should probably be avoided, as all buttons have native scrolling built in
function void PDA_InitScrollingButton (PDAButton button, int incat, int catpage, str message, int onlyVisual, int accelScroll) {
    if (incat > 0 && PDA_Local.page == catpage) {
        button.state = button_hovered;
        button.alpha = 1;
    }

    // TODO: Move buttonBoundsDefault call here, but avoid repeated sound spam and mouse state glitchiness

    if (!onlyVisual) {
        switch (button.state) {
            case button_clicked:
                if (!incat) {
                    PDA_SetCategory(catpage);
                    if(PDA_Local.category != PDA_ASSEMBLY_DETAIL) {
                        PDA_Local.page = 1;
                    }
                }
                if (incat) {
                    if(PDA_Local.category != 0 && PDA_Local.page >= 0) {
                        PDA_Local.page = catpage;
                        PDA_Local.scrollingy = 20.0;
                    }
                }
                PDA_LocalMouse.state = mouse_default;
                break;
            case button_hovered:
                if (strcmp(message, "") != 0) {
                    SetFont(PDA_FONT_SLIM);
                    SetHudClipRect(
                        PDA_MENU_TOOLTIP_X,     PDA_MENU_TOOLTIP_Y,
                        PDA_MENU_TOOLTIP_WIDTH, PDA_MENU_TOOLTIP_HEIGHT,
                        PDA_MENU_TOOLTIP_WIDTH
                    );
                    HudMessage(s:message; HUDMSG_PLAIN, PDA_CAT_DESCRIPTION, CR_UNTRANSLATED,
                    PDA_MENU_TOOLTIP_X + X_LEFT_EDGE, PDA_MENU_TOOLTIP_Y + Y_TOP_EDGE, 0);
                    PDA_SetupScreenBounds();
                }
                break;
        }
    }

    // Handle the button rendering here
    button.alpha = (button.state == button_hovered) ? 1 : 0.6;
    fixed xlabeloff = (strcmp(button.graphic, "PTAB") == 0) ? 4.0 : 0.0;
    fixed ylabeloff = (strcmp(button.graphic, "PTAB") == 0) ? 3.0 : 0.0;

    // SetFont(button.graphic);
    // HudMessageParallax("A\n", button.graphicFlags, button.graphicID, CR_UNTRANSLATED, button.graphicX, button.graphicY, 0, button.alpha, accelScroll);
    PrintSpriteAlpha(button.graphic, button.graphicID, button.graphicX, button.graphicY, 0, button.alpha);

    SetFont(button.textFont);
    HudMessage(s:button.label; button.textFlags, button.textID, CR_UNTRANSLATED, button.finalX, button.finalY, 0, button.alpha, accelScroll);
}

// Setup scrolling arrows for pages with lots of text
function void PDA_SetScrollArrows (int scrollDownMax) {
	if(PDA_Local.scrollingy >= 0)
		DisplayIcon("RLZARU", PDA_SCROLLUPID, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_HEIGHT + Y_TOP_EDGE, 0, 0.0);
	else
		DisplayIcon("RLZARU", PDA_SCROLLUPID, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_Y + 10.0 + Y_TOP_EDGE, 0, 0.5);


	if(PDA_Local.scrollingy <= scrollDownMax)
		DisplayIcon("RLZARD", PDA_SCROLLDNID, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_HEIGHT + Y_TOP_EDGE, 0, 0.0);
	else
		DisplayIcon("RLZARD", PDA_SCROLLDNID, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_HEIGHT + Y_TOP_EDGE, 0, 0.5);
}

function void PDA_SetScrollArrows2 (int scrollDownMax, float x, float y) {
    bool notbot = PDA_Local.scrollingy > scrollDownMax;
    bool nottop = PDA_Local.scrollingy < 0;

    if(notbot) DisplayIcon("RLZARD", PDA_SCROLLDNID, x, y, 0, 0.5);
    if(nottop) DisplayIcon("RLZARU", PDA_SCROLLUPID, x, PDA_SCREEN_Y + 40.0 + Y_TOP_EDGE, 0, 0.5);
}

// THEN ERASE THEM FROM REALITY
function void PDA_UnsetScrollArrows (void) {
	DisplayIcon("RLZARU", PDA_SCROLLUPID, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_Y + 10.0 + Y_TOP_EDGE, 0, 0);
	DisplayIcon("RLZARD", PDA_SCROLLDNID, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_HEIGHT + 0.0 + Y_TOP_EDGE, 0, 0);
}

function void PDA_SetScrollWindow (int scrollDownMax) {
	// SetHudClipRect(PDA_SCREEN_X, PDA_SCROLLABLE_Y, PDA_SCREEN_WIDTH, PDA_SCROLLABLE_HEIGHT, PDA_SCREEN_WIDTH);
    PDA_SetupScreenBounds();
	PDA_SetScrollArrows2(scrollDownMax, PDA_SCREEN_WIDTH + X_LEFT_EDGE, PDA_SCREEN_HEIGHT + Y_TOP_EDGE);
}

function void PDA_UnsetScrollWindow (void) {
	SetHudClipRect(PDA_SCREEN_X, PDA_SCREEN_Y, PDA_SCREEN_WIDTH, PDA_SCREEN_HEIGHT, PDA_SCREEN_WIDTH);
}

// Instead of clogging up the script, just do it here.
function void PDA_EstablishModLimits (void) {
	if (CheckInventory("RLStandardWeaponToken") || CheckInventory("RLExoticWeaponToken")) {
		PDA_Local.maxmodlimit = 4;
  }

	if (CheckInventory("RLSuperiorWeaponToken") || CheckInventory("RLBasicAssemblyTypeWeapon")) {
		PDA_Local.maxmodlimit = 2;
  }

	if (CheckInventory("RLUniqueWeaponToken") || CheckInventory("RLDemonicWeaponToken") || CheckInventory("RLLegendaryWeaponToken") || CheckInventory("RLAdvancedAssemblyTypeWeapon")) {
		if (StrCmp(GetWeapon(),"RLDuke2Rifle") == 0 || StrCmp(GetWeapon(),"RLChameleonRifle") == 0 || StrCmp(GetWeapon(),"RLWidowmakerSMG") == 0)
			PDA_Local.maxmodlimit = 3;
		else
			PDA_Local.maxmodlimit = 1;
  }

	if (CheckInventory("RLMasterAssemblyTypeWeapon") || CheckInventory("RLSuperiorAssembledWeaponToken")) {
		PDA_Local.maxmodlimit = 0;
  }
}


function PDA_ArmorData setupArmorData (int specialArmorTokens) {
    #define armorActor DRLA_ArmorList[localArmorIndex][armor_ACTOR]

    bool isRenegade = false;

    if(CheckInventory("DRPGDRLAActive")) isRenegade = (CheckActorClass(0,"DoomRLRenegadeRPG") == true);
    else isRenegade = (CheckActorClass(0,"DoomRLRenegade") == true);

    if (specialArmorTokens) {
        if (CheckInventory("RLEnclaveSetBonusActive")) localArmorData.langEntry = strParam("PDA_ARMOR_%s:COMPLETE", armorActor);
        else                                           localArmorData.langEntry = strParam("PDA_ARMOR_BOOSTED_%s:", armorActor);

        localArmorData.armorRes  = strParam("%l:", strParam("%l:_RES", localArmorData.langEntry));
    } else {
        localArmorData.langEntry = strParam("PDA_ARMOR_%s:", armorActor);
        localArmorData.armorRes  = strParam("%l:", strParam("%l:_RES", localArmorData.langEntry));

        if(CheckInventory("PowerRLReactiveShieldSystemMelee"))
            localArmorData.armorRes = "PDA_ARMOR_REACTIVESHIELDSYSTEM_MELEE";

        if(CheckInventory("PowerRLReactiveShieldSystemBullet"))
            localArmorData.armorRes = "PDA_ARMOR_REACTIVESHIELDSYSTEM_BULLET";

        if(CheckInventory("PowerRLReactiveShieldSystemFire"))
            localArmorData.armorRes = "PDA_ARMOR_REACTIVESHIELDSYSTEM_FIRE";

        if(CheckInventory("PowerRLReactiveShieldSystemPlasma"))
            localArmorData.armorRes = "PDA_ARMOR_REACTIVESHIELDSYSTEM_PLASMA";
    }

    if (specialArmorTokens && stricmp(strLeft(DRLA_ArmorList[localArmorIndex][armor_TOKEN], 17), "RLCyberneticArmor") == 0) {
        localArmorData.name      = "PDA_ARMOR_CYBERNETICARMOR_NAME";
        localArmorData.graphic   = "PDA_ARMOR_CYBERNETICARMOR_ICON";
        localArmorData.armorAttr = "PDA_ARMOR_CYBERNETICARMOR_ATTR";
        localArmorData.armorDesc = "PDA_ARMOR_CYBERNETICARMOR_DESC";
        localArmorData.armorVal  = strParam("%l:\n\n",
                (!isRenegade) ? "PDA_ARMOR_CYBERNETICARMOR_PROT" : "PDA_ARMOR_CYBERNETICARMOR_RENPROT");
        localArmorData.tooltip   = strParam("%l:\n%l:\n%l:",
                (!isRenegade) ? "PDA_ARMOR_CYBERNETICARMOR_PROT" : "PDA_ARMOR_CYBERNETICARMOR_RENPROT", localArmorData.armorRes, localArmorData.armorAttr);

        if (CheckInventory("RLCyberneticArmorModdedToken")) {
            if (CheckInventory("RLCyberneticArmorBulkToken"))      localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORBULK_RES";
            if (CheckInventory("RLCyberneticArmorPowerToken"))     localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORPOWER_RES";
            if (CheckInventory("RLCyberneticArmorAgilityToken"))   localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORAGILITY_RES";
            if (CheckInventory("RLCyberneticArmorTechnicalToken")) localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORTECHNICAL_RES";
            if (CheckInventory("RLCyberneticArmorSniperToken"))    localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORSNIPER_RES";
            if (CheckInventory("RLCyberneticArmorFirestormToken")) localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORFIRESTORM_RES";
            if (CheckInventory("RLCyberneticArmorNanoToken"))      localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORNANO_RES";
            if (CheckInventory("RLCyberneticArmorOnyxToken"))      localArmorData.armorRes = "PDA_ARMOR_BOOSTED_CYBERNETICARMORONYX_RES";
        }
    } else {
        localArmorData.name      = strParam("%l:", strParam("%l:_NAME", localArmorData.langEntry));
        localArmorData.graphic   = strParam("%l:", strParam("%l:_ICON", localArmorData.langEntry));
        localArmorData.armorAttr = strParam("%l:", strParam("%l:_ATTR", localArmorData.langEntry));
        localArmorData.armorDesc = strParam("%l:", strParam("%l:_DESC", localArmorData.langEntry));
        localArmorData.armorVal  = strParam("%l:\n\n",
                    (!isRenegade) ? strParam("%l:_PROT", localArmorData.langEntry) : strParam("%l:_RENPROT", localArmorData.langEntry));
        localArmorData.tooltip   = strParam("%l:\n%l:\n%l:",
                    (!isRenegade) ? strParam("%l:_PROT", localArmorData.langEntry) : strParam("%l:_RENPROT", localArmorData.langEntry),
                    localArmorData.armorRes, localArmorData.armorAttr);
    }

    return localArmorData;
}


function PDA_BootsData setupBootsData (void) {
    #define bootsActor DRLA_ArmorList[localBootsIndex][armor_ACTOR]

    localBootsData.langEntry = strParam("PDA_ARMOR_%s:", bootsActor);

    // if(CheckInventory("RLEnclaveSetBonusActive")) {
    //     localBootsData.langEntry = strParam("PDA_ARMOR_BOOSTED_%s:", bootsActor);
    // } else  {
    //     localBootsData.langEntry = strParam("PDA_ARMOR_%s:", bootsActor);
    // }
    localBootsData.bootsVal = strParam("%l:\n\n", strParam("%l:_PROT", CheckInventory("RLEnclaveSetBonusActive") ? strParam("PDA_ARMOR_BOOSTED_%s:", bootsActor) : localBootsData.langEntry));
    // if (CheckInventory("RLEnclaveSetBonusActive")) {
    //     localBootsData.bootsVal = strParam("%l:\n\n",  strParam("%l:_PROT", strParam("PDA_ARMOR_BOOSTED_%s:", bootsActor)));
        // langEntry = strParam("PDA_ARMOR_BOOSTED_%s:", bootsActor);

    // } else {
    //     localBootsData.bootsVal = strParam("%l:\n\n",  strParam("%l:_PROT", localBootsData.langEntry));
        // langEntry = strParam("PDA_ARMOR_%s:", bootsActor);
    // }

    localBootsData.name      = strParam("%l:", strParam("%l:_NAME", localBootsData.langEntry));
    localBootsData.graphic   = strParam("%l:", strParam("%l:_ICON", localBootsData.langEntry));
    localBootsData.bootsAttr = strParam("%l:", strParam("%l:_ATTR", localBootsData.langEntry));
    localBootsData.bootsDesc = strParam("%l:", strParam("%l:_DESC", localBootsData.langEntry));
    localBootsData.tooltip   = strParam("%l:\n\n%l:", strParam("%l:_PROT", localBootsData.langEntry), localBootsData.bootsAttr);

    return localBootsData;
}


function PDA_BackpackData setupBackpackData (void) {
    #define backpackIndex DRLA_BackpackList[localBackpackIndex][1]

    localBackpackData.name    = strParam("%l:", strParam("PDA_BACKPACK_%s:_NAME", backpackIndex));
    localBackpackData.graphic = strParam("%l:", strParam("PDA_BACKPACK_%s:_ICON", backpackIndex));
    localBackpackData.tooltip = "";

    localBackpackData.backpackVal = strParam("%l:", strParam("PDA_BACKPACK_%s:_DESC", backpackIndex));

    return localBackpackData;
}